import com.android.builder.core.VariantType

group = 'hu.nemi'
version = "0.1"

apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    testOptions {
        unitTests.returnDefaultValues = true
    }

    defaultConfig {
        minSdkVersion 8
        targetSdkVersion 23
        versionCode 1
        versionName "0.1"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    provided 'com.android.support:appcompat-v7:23.1.1'

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-all:1.10.19'
}


android.libraryVariants.all { variant ->
    def variantData = variant.getVariantData()
    def testVariantData = variantData.getTestVariantData(VariantType.UNIT_TEST)
    if(testVariantData != null) {
        def runTestTaskName = testVariantData.getScope().getTaskName(VariantType.UNIT_TEST.getPrefix())
        def runTestTask = project.getTasks().findByName(runTestTaskName);
        runTestTask.classpath = testVariantData.javacTask.getOutputs().getFiles() + runTestTask.classpath
    }
}


def siteUrl = 'https://github.com/mos8502/viewdecorator'
def gitUrl = 'https://github.com/mos8502/viewdecorator.git'

// properties are loaded from local.properties, not commited/pushed
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty('bintrayUser')
    key  = properties.getProperty('bintrayKey')
    configurations = ['archives']
    pkg {
        repo = 'playground'
        name = project.name
        desc = 'View Decorator library for Android'
        websiteUrl = siteUrl
        issueTrackerUrl = 'https://github.com/mos8502/viewdecorator/issues'
        vcsUrl = gitUrl
        licenses = ['Apache-2.0']
        labels = ['aar', 'android']
	publish = true
    }
}


